plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id "me.qoomon.git-versioning" version "2.1.1"
    id 'org.jetbrains.dokka' version '1.7.20'
}

apply plugin: "maven-publish"
group 'cf.wayzer'
version '3.1'

gitVersioning.apply {
    tag {
        pattern = "v(?<tagVersion>[0-9].*)"
        versionFormat = "\${tagVersion}"
    }
    commit {
        versionFormat = "\${commit.short}-SNAPSHOT"
    }
}

repositories {
    maven { url "https://maven.aliyun.com/repository/public" }
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    from dokkaJavadoc.outputDirectory
    archiveClassifier.set("javadoc")
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact kotlinSourcesJar
//            artifact javadocJar
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
        }
    }
}
